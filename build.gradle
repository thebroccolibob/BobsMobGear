plugins {
	id 'fabric-loom' version '1.10-SNAPSHOT'
	id 'maven-publish'
	id "org.jetbrains.kotlin.jvm" version "2.1.20"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.

	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
	maven { url "https://maven.shedaniel.me/" }
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name "KosmX's maven"
		url 'https://maven.kosmx.dev/'
	}
	maven { url = "https://maven.bawnorton.com/releases" }
	maven {
		name "Ladysnake"
		url "https://maven.ladysnake.org/releases"
	}
	maven {
		name = "Greenhouse Maven"
        url = "https://maven.greenhouse.lgbt/releases"
	}
	maven {
		name = "Greenhouse Maven"
		url = 'https://repo.greenhouse.house/snapshots/' // Porting Lib Hotfixes
	}
	maven { url "https://mvn.devos.one/snapshots/" } // Porting Lib Betas
	maven {
		url "https://jitpack.io/" // Fabric ASM
		content {
			excludeGroup "io.github.fabricators_of_create"
		}
	}
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"bobsmobgear" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

	accessWidenerPath = file("src/main/resources/bobsmobgear.accesswidener")
}

fabricApi {
	configureDataGeneration {
		client = true
		modId = "bobsmobgear"
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

	include(implementation(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-fabric:${project.mixinsquared_version}")))
	modImplementation "maven.modrinth:better-combat:${project.better_combat_version}-fabric"
	modImplementation("maven.modrinth:pommel-held-item-models:${project.pommel_version}")
	modImplementation("maven.modrinth:flexible-arms-1.21:${project.flexible_arms_version}")
	modImplementation("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modImplementation "dev.kosmx.player-anim:player-animation-lib-fabric:${project.player_anim_version}"

	modCompileOnly "dev.emi:emi-fabric:${project.emi_version}:api"
	modLocalRuntime "dev.emi:emi-fabric:${project.emi_version}"

	modCompileOnly "com.github.ThePotatoArchivist:RPGSkills:${project.rpgskills_version}"

	modLocalRuntime "maven.modrinth:bundle-api:${project.bundle_api_version}"
	modLocalRuntime "maven.modrinth:runes:${project.runes_version}"
	modLocalRuntime "maven.modrinth:ranged-weapon-api:${project.ranged_weapon_api_version}"
	modLocalRuntime modCompileOnly("maven.modrinth:spell-engine:${project.spell_engine_version}")
	modLocalRuntime "maven.modrinth:spell-power:${project.spell_power_version}"
	modLocalRuntime "maven.modrinth:azurelib-armor:${project.azurelib_armor_version}"
	modLocalRuntime "maven.modrinth:structure-pool-api:${project.structure_pool_api_version}"
	modLocalRuntime "maven.modrinth:shield-api:${project.shield_api_version}"
	modLocalRuntime "dev.emi:trinkets:${project.trinkets_version}"
//	modLocalRuntime modCompileOnly("maven.modrinth:wizards:${project.rpg_wizards_version}")
	modLocalRuntime modCompileOnly("maven.modrinth:paladins-and-priests:${project.rpg_paladins_priests_version}")
	modLocalRuntime modCompileOnly("maven.modrinth:archers:${project.rpg_archers_version}")
	modLocalRuntime modCompileOnly("maven.modrinth:rogues-and-warriors:${project.rpg_rogues_warriors_version}")

	modLocalRuntime modCompileOnly("vectorwing:FarmersDelight:${fdrf_version}") {
		exclude(group: "net.fabricmc")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = 21
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
